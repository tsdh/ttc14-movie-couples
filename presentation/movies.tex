\documentclass{beamer}

\usepackage[T1]{fontenc}
\usepackage{url}
\usepackage{hyperref}
\usepackage{minted}
\newminted{clojure}{fontsize=\fontsize{8}{8},linenos,numbersep=3pt}
\newmintinline{clojure}{fontsize=\fontsize{8}{8}}
\newcommand{\code}{\clojureinline}

\usetheme{AnnArbor}

\title{Solving the TTC Movie Database Case with FunnyQT}
\author{Tassilo Horn\\
  \href{mailto:horn@uni-koblenz.de}{horn@uni-koblenz.de}}

\begin{document}
\maketitle{}

\section{Task 1: Generating Synthetic Models}

\begin{frame}
  \LARGE
  \begin{center}
    \textbf{Generating Synthetic Models}
  \end{center}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Implementation: create-positive! function}
  \begin{clojurecode*}{fontsize=\tiny}
(defn ^:private create-positive! [model i]
  (let [m1 (ecreate! model 'Movie :rating (+ 0.0 (* 10 i)))
        m2 (ecreate! model 'Movie :rating (+ 1.0 (* 10 i)))
        m3 (ecreate! model 'Movie :rating (+ 2.0 (* 10 i)))
        m4 (ecreate! model 'Movie :rating (+ 3.0 (* 10 i)))
        m5 (ecreate! model 'Movie :rating (+ 4.0 (* 10 i)))]
    (ecreate! model 'Actor   :name (str "a" (* 10 i))       :movies [m1 m2 m3 m4])
    (ecreate! model 'Actor   :name (str "a" (+ 1 (* 10 i))) :movies [m1 m2 m3 m4])
    (ecreate! model 'Actor   :name (str "a" (+ 2 (* 10 i))) :movies [m2 m3 m4])
    (ecreate! model 'Actress :name (str "a" (+ 3 (* 10 i))) :movies [m2 m3 m4 m5])
    (ecreate! model 'Actress :name (str "a" (+ 4 (* 10 i))) :movies [m2 m3 m4 m5])))
  \end{clojurecode*}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Implementation: create-negative! function}
  \begin{clojurecode*}{fontsize=\tiny}
(defn ^:private create-negative! [model i]
  (let [m1 (ecreate! model 'Movie :rating (+ 5.0 (* 10 i)))
        m2 (ecreate! model 'Movie :rating (+ 6.0 (* 10 i)))
        m3 (ecreate! model 'Movie :rating (+ 7.0 (* 10 i)))
        m4 (ecreate! model 'Movie :rating (+ 8.0 (* 10 i)))
        m5 (ecreate! model 'Movie :rating (+ 9.0 (* 10 i)))]
    (ecreate! model 'Actor   :name (str "a" (+ 5 (* 10 i))) :movies [m1 m2])
    (ecreate! model 'Actor   :name (str "a" (+ 6 (* 10 i))) :movies [m1 m2 m3])
    (ecreate! model 'Actress :name (str "a" (+ 7 (* 10 i))) :movies [m2 m3 m4])
    (ecreate! model 'Actress :name (str "a" (+ 8 (* 10 i))) :movies [m3 m4 m5])
    (ecreate! model 'Actress :name (str "a" (+ 9 (* 10 i))) :movies [m4 m5])))
  \end{clojurecode*}
\end{frame}

\begin{frame}[fragile]
\frametitle{Implementation: Calling the functions}
  \begin{clojurecode}
(defn create-example [n]
  (let [model (new-resource)]
    (dotimes [i n]
      (create-positive! model i)
      (create-negative! model i))
    model))
  \end{clojurecode}
\end{frame}

\section{(Extension) Tasks 2 and 3: Finding Couples/Cliques}

\begin{frame}
  \LARGE
  \begin{center}
    \textbf{(Extension) Tasks 2 and 3: Finding Couples/Cliques}
  \end{center}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Task 2/3: Finding Couples}
  \begin{clojurecode}
(defrule make-groups-of-2!
  {:forall true, :no-result-vec true}
  [model c]
  [m<Movie>          :when (>= (person-count m) 2)
   m -<persons>-> p0 :when (>= (movie-count p0) c)
   m -<persons>-> p1 :when (>= (movie-count p1) c)
   :when (neg? (compare (eget p0 :name)
                        (eget p1 :name)))
   :when-let [cms (n-common-movies? c p0 p1)]
   :as [cms p0 p1]
   :distinct]
  (ecreate! model 'Couple :p1 p0 :p2 p1
            :commonMovies cms :avgRating (avg-rating cms)))
  \end{clojurecode}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Extension Task 2/3: Finding 3-Cliques}
  \begin{clojurecode}
(defrule make-groups-of-3!
  {:forall true, :no-result-vec true}
  [model c]
  [m<Movie>          :when (>= (person-count m) 3)
   m -<persons>-> p0 :when (>= (movie-count p0) c)
   m -<persons>-> p1 :when (>= (movie-count p1) c)
   :when (neg? (compare (eget p0 :name)
                        (eget p1 :name)))
   :when (n-common-movies? c p0 p1)
   m -<persons>-> p2 :when (>= (movie-count p2) c)
   :when (neg? (compare (eget p1 :name)
                        (eget p2 :name)))
   :when-let [cms (n-common-movies? c p0 p1 p2)]
   :as [cms p0 p1 p2]
   :distinct]
  (ecreate! model 'Clique :persons [p0 p1 p2]
           :commonMovies cms :avgRating (avg-rating cms)))
  \end{clojurecode}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Generating the Rules as HOT}
  \begin{clojurecode*}{fontsize=\tiny}
(defmacro define-group-rule [n]
  (let [psyms (map #(symbol (str "p" %)) (range n))]
    `(defrule ~(symbol (str "make-groups-of-" n "!"))
       {:forall true :no-result-vec true}
       [~'model ~'c]
       [~'m<Movie> :when (>= (person-count ~'m) ~n)
        ~@(mapcat (fn [i]
                    (let [ps (nth psyms i)]
                      `[~'m -<persons>-> ~ps
                        :when (>= (movie-count ~ps) ~'c)
                        ~@(when-not (zero? i)
                            `[:when (neg? (compare (eget ~(nth psyms (dec i)) :name)
                                                   (eget ~ps :name)))])
                        ~@(when-not (or (zero? i) (= i (dec n)))
                            `[:when (n-common-movies? ~'c ~@(take (inc i) psyms))])]))
                  (range n))
        :when-let [~'cms (n-common-movies? ~'c ~@psyms)]
        :as [~'cms ~@psyms]
        :distinct]
       (ecreate! ~'model ~@(if (= n 2)
                                 `['Couple :p1 ~(first psyms) :p2 ~(second psyms)]
                                 `['Clique :persons [~@psyms]])
                 :commonMovies ~'cms :avgRating (avg-rating ~'cms)))))

(define-group-rule 2) ;; Generate the Couples rule
(define-group-rule 3) ;; Generate the 3-Cliques rule
(define-group-rule 4) ;; Generate the 4-Cliques rule
(define-group-rule 5) ;; Generate the 5-Cliques rule
  \end{clojurecode*}
\end{frame}

\section{Extension Tasks 1/4: Compute Top-15 Couples/Cliques}

\begin{frame}
  \LARGE
  \begin{center}
    \textbf{Extension Tasks 1/4: Compute Top-15 Couples/Cliques}
  \end{center}
\end{frame}

\begin{frame}
  \frametitle{Comparators}
  \begin{enumerate}
  \item Average Rating
  \item Number of Common Movies
  \item Lexicograpic Order of Actor Names
  \end{enumerate}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Implementation of Comparators}
  \begin{clojurecode}
(defn rating-comparator [a b]
  (compare (eget b :avgRating)
           (eget a :avgRating)))

(defn common-movies-comparator [a b]
  (compare (.size ^java.util.Collection (eget b :commonMovies))
           (.size ^java.util.Collection (eget a :commonMovies))))
  \end{clojurecode}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Implementation of Comparators}
  \begin{clojurecode}
(declare-polyfn actors [group])

(defpolyfn actors movies.Couple [group]
  [(eget group :p1) (eget group :p2)])

(defpolyfn actors movies.Clique [group]
  (emf/eget-raw group :persons))

(defn names-comparator [a b]
  (compare (join ";" (map #(eget % :name) (actors a)))
           (join ";" (map #(eget % :name) (actors b)))))
  \end{clojurecode}
\end{frame}

\begin{frame}[fragile]
  \frametitle{Comparator Combinator}
  \begin{clojurecode}
(defn comparator-combinator [& comparators]
  (fn [a b]
    (loop [cs comparators]
      (if (seq cs)
        (let [r ((first cs) a b)]
          (if (zero? r)
            (recur (rest cs))
            r))
        (u/errorf "%s and %s are incomparable!" a b)))))
  \end{clojurecode}
\end{frame}

\begin{frame}[fragile]
  \frametitle{The Top-15 Metrics}
  \begin{clojurecode}
(defn groups-by-avg-rating [groups]
  (sort (comparator-combinator rating-comparator common-movies-comparator
                               names-comparator)
        groups))

(defn groups-by-common-movies [groups]
  (sort (comparator-combinator common-movies-comparator rating-comparator
                               names-comparator)
        groups))
  \end{clojurecode}
\end{frame}

\section{Performance}

\begin{frame}
  \LARGE
  \begin{center}
    \textbf{Performance}
  \end{center}
\end{frame}

\begin{frame}
  \frametitle{Performance: Synthetic Models}
  \footnotesize
  \begin{table}
    \centering
    \begin{tabular}{| r | r | r | r | r | r |}
      \hline
      \textbf{Model (N)} & \textbf{Gen} & \textbf{Couples} & \textbf{3-Cliques} & \textbf{4-Cliques} & \textbf{5-Cliques}\\
      \hline
      1000   &    0.12 & 0.274602528  &  0.349061152  &  0.483500704  &  0.422209712\\
      2000   &    0.23 & 0.547466160  &  0.722435888  &  0.724224848  &  0.628254896\\
      3000   &    0.29 & 0.867141568  &  1.069310832  &  1.028428128  &  0.949939408\\
      4000   &    0.39 & 1.119036992  &  1.437665888  &  1.375804384  &  1.216168544\\
      5000   &    0.49 & 1.406045888  &  1.798415536  &  1.723140656  &  1.502462976\\
      10000  &    0.96 & 2.830416128  &  3.607990864  &  3.482801824  &  3.098102832\\
      50000  &    4.82 & 14.324653712 &  17.972272736 &  17.346692096 &  15.327367392\\
      100000 &    9.62 & 28.300907200 &  36.352164048 &  35.396692384 &  31.440208448\\
      200000 &   20.35 & 57.159804192 &  72.209621472 &  69.233912672 &  60.430165808\\
      \hline
    \end{tabular}
  \end{table}
\end{frame}

\begin{frame}
  \frametitle{Performance: IMDb Models}
  \begin{table}
    \centering
    \begin{tabular}{| l | r | r |}
      \hline
      \textbf{Model}                  & \textbf{Couples} & \textbf{3-Cliques}\\
      \hline
      imdb-0005000-49930.movies.bin   & 1.15      & 7.473   \\
      imdb-0010000-98168.movies.bin   & 1.81      & 12.584  \\
      imdb-0030000-207420.movies.bin  & 2.86      & 13.959  \\
      imdb-0045000-299504.movies.bin  & 4.11      & 16.917  \\
      imdb-0065000-404920.movies.bin  & 6.67      & 24.248  \\
      imdb-0085000-499995.movies.bin  & 10.47     & 30.936  \\
      imdb-0130000-709551.movies.bin  & 21.16     & 63.162  \\
      imdb-0200000-1004463.movies.bin & 40.06     & 126.58  \\
      imdb-0340000-1505143.movies.bin & 96.63     & 385.14  \\
      imdb-0495000-2000900.movies.bin & 173.75    & 770.857 \\
      imdb-0660000-2501893.movies.bin & 283.38    & 1524.507\\
      imdb-all-3257145.movies.bin     & 576.75    & 4675.072\\
      \hline
    \end{tabular}
  \end{table}
\end{frame}

\end{document}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: t
%%% TeX-engine: pdflatex-shell-escape
%%% End:
